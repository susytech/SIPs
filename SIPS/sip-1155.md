---
sip: 1155
title: SRC-1155 Multi Token Standard
author: Witek Radomski <witek@enjin.com>, Andrew Cooke <ac0dem0nk3y@gmail.com>, Philippe Castonguay <pc@horizongames.net>, James Therien <james@enjin.com>, Eric Binet <eric@enjin.com>
type: Standards Track
category: SRC
status: Draft
review-period-end: 2019-03-28
created: 2018-06-17
discussions-to: https://github.com/susytech/SIPs/issues/1155
requires: 165
---

## Simple Summary

A standard interface for contracts that manage multiple token types. A single deployed contract may include any combination of fungible tokens, non-fungible tokens, or other configurations (for example, semi-fungible tokens).

## Abstract

This standard outlines a smart contract interface that can represent any number of Fungible and Non-Fungible token types. Existing standards such as SRC-20 require deployment of separate contracts per token type. The SRC-721 standard's Token ID is a single non-fungible index and the group of these non-fungibles is deployed as a single contract with settings for the entire collection. In contrast, the SRC-1155 Multi Token Standard allows for each Token ID to represent a new configurable token type, which may have its own metadata, supply and other attributes.

The `_id` parameter is contained in each function's parameters and indicates a specific token or token type in a transaction.

## Motivation

Tokens standards like SRC-20 and SRC-721 require a separate contract to be deployed for each token type or collection. This places a lot of redundant bytecode on the Susy blockchain and limits certain functionality by the nature of separating each token contract into its own permissioned address. With the rise of blockchain games and platforms like Enjin Coin, game developers may be creating thousands of token types, and a new type of token standard is needed to support them. However, SRC-1155 is not specific to games, and many other applications can benefit from this flexibility.

New functionality is possible with this design, such as transferring multiple token types at once, saving on transaction costs. Trading (escrow / atomic swaps) of multiple tokens can be built on top of this standard and it removes the need to "approve" individual token contracts separately. It is also easy to describe and mix multiple fungible or non-fungible token types in a single contract.

## Specification

The key words "MUST", "MUST NOT", "REQUIRED", "SHALL", "SHALL NOT", "SHOULD", "SHOULD NOT", "RECOMMENDED", "MAY", and "OPTIONAL" in this document are to be interpreted as described in RFC 2119.

**Smart contracts implementing the SRC-1155 standard MUST implement the `SRC1155` and `SRC165` interfaces.**

```solidity
pragma solidity ^0.5.8;

/**
    @title SRC-1155 Multi Token Standard
    @dev See https://sips.superstring.ch/SIPS/sip-1155
    Note: The SRC-165 identifier for this interface is 0xd9b67a26.
 */
interface SRC1155 /* is SRC165 */ {
    /**
        @dev Either TransferSingle or TransferBatch MUST emit when tokens are transferred, including zero value transfers as well as minting or burning.
        Operator MUST be msg.sender.
        When minting/creating tokens, the `_from` field MUST be set to `0x0`
        When burning/destroying tokens, the `_to` field MUST be set to `0x0`
        The total value transferred from address 0x0 minus the total value transferred to 0x0 MAY be used by clients and exchanges to be added to the "circulating supply" for a given token ID.
        To broadcast the existence of a token ID with no initial balance, the contract SHOULD emit the TransferSingle event from `0x0` to `0x0`, with the token creator as `_operator`, and a `_value` of 0.
    */
    event TransferSingle(address indexed _operator, address indexed _from, address indexed _to, uint256 _id, uint256 _value);
    
    /**
        @dev Either TransferSingle or TransferBatch MUST emit when tokens are transferred, including zero value transfers as well as minting or burning.
        Operator MUST be msg.sender.
        When minting/creating tokens, the `_from` field MUST be set to `0x0`
        When burning/destroying tokens, the `_to` field MUST be set to `0x0`
        The total value transferred from address 0x0 minus the total value transferred to 0x0 MAY be used by clients and exchanges to be added to the "circulating supply" for a given token ID.
        To broadcast the existence of multiple token IDs with no initial balance, this SHOULD emit the TransferBatch event from `0x0` to `0x0`, with the token creator as `_operator`, and a `_value` of 0.
    */
    event TransferBatch(address indexed _operator, address indexed _from, address indexed _to, uint256[] _ids, uint256[] _values);

    /**
        @dev MUST emit when approval for a second party/operator address to manage all tokens for an owner address is enabled or disabled (absense of an event assumes disabled).        
    */
    event ApprovalForAll(address indexed _owner, address indexed _operator, bool _approved);

    /**
        @dev MUST emit when the URI is updated for a token ID.
        URIs are defined in RFC 3986.
        The URI MUST point a JSON file that conforms to the "SRC-1155 Metadata JSON Schema".
    */
    event URI(string _value, uint256 indexed _id);

    /**
        @notice Transfers value amount of an _id from the _from address to the _to address specified.
        @dev MUST emit TransferSingle event on success.
        Caller must be approved to manage the _from account's tokens (see "Approval" section of the standard).
        MUST revert if `_to` is the zero address.
        MUST revert if balance of sender for token `_id` is lower than the `_value` sent.
        MUST revert on any other error.
        After the transfer succeeds, this function MUST check if `_to` is a smart contract (eg. code size > 0). If so, it MUST call `onSRC1155Received` on `_to` and act appropriately (see "Safe Transfer Rules" section of the standard).
        @param _from    Source address
        @param _to      Target address
        @param _id      ID of the token type
        @param _value   Transfer amount
        @param _data    Additional data with no specified format, sent in call to `onSRC1155Received` on `_to`
    */
    function safeTransferFrom(address _from, address _to, uint256 _id, uint256 _value, bytes calldata _data) external;

    /**
        @notice Send multiple types of Tokens from a 3rd party in one transfer (with safety call).
        @dev MUST emit TransferBatch event on success.
        Caller must be approved to manage the _from account's tokens (see "Approval" section of the standard).
        MUST revert if `_to` is the zero address.
        MUST revert if length of `_ids` is not the same as length of `_values`.
        MUST revert if any of the balance of sender for token `_ids` is lower than the respective `_values` sent.
        MUST revert on any other error.
        Transfers and events MUST occur in the array order they were submitted (_ids[0] before _ids[1], etc).
        After all the transfer(s) in the batch succeed, this function MUST check if `_to` is a smart contract (eg. code size > 0). If so, it MUST call `onSRC1155BatchReceived` on `_to` and act appropriately (see "Safe Transfer Rules" section of the standard).        
        @param _from    Source addresses
        @param _to      Target addresses
        @param _ids     IDs of each token type (order and length must match _values array)
        @param _values  Transfer amounts per token type (order and length must match _ids array)
        @param _data    Additional data with no specified format, sent in call to `onSRC1155BatchReceived` on `_to`
    */
    function safeBatchTransferFrom(address _from, address _to, uint256[] calldata _ids, uint256[] calldata _values, bytes calldata _data) external;

    /**
        @notice Get the balance of an account's Tokens.
        @param _owner  The address of the token holder
        @param _id     ID of the Token
        @return        The _owner's balance of the Token type requested
     */
    function balanceOf(address _owner, uint256 _id) external view returns (uint256);
    
    /**
        @notice Get the balance of multiple account/token pairs
        @param _owners The addresses of the token holders
        @param _ids    ID of the Tokens
        @return        The _owner's balance of the Token types requested (i.e. balance for each (owner, id) pair)
     */
    function balanceOfBatch(address[] calldata _owners, uint256[] calldata _ids) external view returns (uint256[] memory);

    /**
        @notice Enable or disable approval for a third party ("operator") to manage all of the caller's tokens.
        @dev MUST emit the ApprovalForAll event on success.
        @param _operator  Address to add to the set of authorized operators
        @param _approved  True if the operator is approved, false to revoke approval
    */
    function setApprovalForAll(address _operator, bool _approved) external;

    /** 
        @notice Queries the approval status of an operator for a given owner.
        @param _owner     The owner of the Tokens
        @param _operator  Address of authorized operator
        @return           True if the operator is approved, false if not
    */
    function isApprovedForAll(address _owner, address _operator) external view returns (bool);
}
```

### SRC-1155 Token Receiver

Smart contracts **MUST** implement this interface to accept transfers. See "Safe Transfer Rules" for further detail.

```solidity
pragma solidity ^0.5.8;

interface SRC1155TokenReceiver {
    /**
        @notice Handle the recsipt of a single SRC1155 token type.
        @dev An SRC1155-compliant smart contract MUST call this function on the token recipient contract, at the end of a `safeTransferFrom` after the balance has been updated.        
        This function MUST return whether it accepts or rejects the transfer via the prescribed keccak256 generated values.
        Return of any other value than the prescribed keccak256 generated values WILL result in the transaction being reverted.
        Note: The contract address is always the message sender.
        @param _operator  The address which initiated the transfer (i.e. msg.sender)
        @param _from      The address which previously owned the token
        @param _id        The id of the token being transferred
        @param _value     The amount of tokens being transferred
        @param _data      Additional data with no specified format
        @return           `bytes4(keccak256("accept_src1155_tokens()"))`==0x4dc21a2f or `bytes4(keccak256("reject_src1155_tokens()"))`==0xafed434d
    */
    function onSRC1155Received(address _operator, address _from, uint256 _id, uint256 _value, bytes calldata _data) external returns(bytes4);
             
    /**
        @notice Handle the recsipt of multiple SRC1155 token types.
        @dev An SRC1155-compliant smart contract MUST call this function on the token recipient contract, at the end of a `safeBatchTransferFrom` after the balances have been updated.        
        This function MUST return whether it accepts or rejects the transfer via the prescribed keccak256 generated values.
        Return of any other value than the prescribed keccak256 generated values WILL result in the transaction being reverted.
        Note: The contract address is always the message sender.
        @param _operator  The address which initiated the batch transfer (i.e. msg.sender)
        @param _from      The address which previously owned the token
        @param _ids       An array containing ids of each token being transferred (order and length must match _values array)
        @param _values    An array containing amounts of each token being transferred (order and length must match _ids array)
        @param _data      Additional data with no specified format
        @return           `bytes4(keccak256("accept_batch_src1155_tokens()"))`==0xac007889 or `bytes4(keccak256("reject_src1155_tokens()"))`==0xafed434d
    */
    function onSRC1155BatchReceived(address _operator, address _from, uint256[] calldata _ids, uint256[] calldata _values, bytes calldata _data) external returns(bytes4);
}
```

### Safe Transfer Rules

To be more explicit about how safeTransferFrom and safeBatchTransferFrom MUST operate, a list of rules follows:

* onSRC1155Received and onSRC1155BatchReceived MUST NOT be called on an EOA account.
* onSRC1155Received and onSRC1155BatchReceived MUST NOT be called outside of a mint or transfer process.

##### When the recipient is a contract:

* The onSRC1155Received hook MUST be called every time one and only one token type is transferred to an address in the transaction.
* The onSRC1155Received hook MUST NOT be called when more than one token type is transferred to an address in the transaction.
* The onSRC1155BatchReceived hook MUST be called when more than one token type is transferred to an address in the transaction with the entire list of what was transferred to it.
* The onSRC1155BatchReceived hook MUST NOT be called when only one token type is transferred to an address in the transaction.

* If implementation specific functions are used to transfer 1155 tokens to a contract the appropriate hook MUST still be called with the same rules as if safeTransferFrom/safeBatchTransferFrom was used.
* If the destination/to contract does not implement the appropriate hook the transfer MUST be reverted with the one caveat below.
  - If the tokens being sent are part of a hybrid implementation of another standard, that particular standard's rules on sending to a contract MAY now be followed instead. See "Compatibility with other standards" section.  

* When calling either onSRC1155Received or onSRC1155BatchReceived:
  - operator MUST be the address of the account/contract that initiated the transfer (i.e. msg.sender).
  - from MUST be the address of the holder whose balance is decreased.
  - to MUST be the address of the recipient whose balance is increased.
  - from MUST be 0x0 for a mint.
  - data MUST contain the extra information provided by the sender (if any) for a transfer.
  - the hook MUST be called after all the balances in the transaction have been updated to match the senders intent.

* When calling onSRC1155Received
  - id MUST be the token type being transferred.
  - value MUST be the number of tokens the holder balance is decreased by and match what the recipient balance is increased by.
  - If the return value is anything other than `bytes4(keccak256("accept_src1155_tokens()"))` or `bytes4(keccack256("reject_src1155_tokens()"))` the transaction MUST be reverted.

* When calling onSRC1155BatchReceived
  - ids MUST be the list of tokens being transferred.
  - values MUST be the list of number of tokens (specified in ids) the holder balance is decreased by and match what the recipient balance is increased by.
  - If the return value is anything other than `bytes4(keccak256("accept_batch_src1155_tokens()"))` or `bytes4(keccack256("reject_src1155_tokens()"))` the transaction MUST be reverted.

* The destination/to contract MAY accept an increase of its balance by returning the acceptance magic value `bytes4(keccak256("accept_src1155_tokens()"))` for onSRC1155Received or `bytes4(keccak256("accept_batch_src1155_tokens()"))` for onSRC1155BatchReceived.
  - If such explicit acceptance happens the transfer MUST be completed, unless other conditions apply.
* The destination/to contract MAY reject an increase of its balance by returning the rejection magic value `bytes4(keccack256("reject_src1155_tokens()"))`.
  - If such explicit rejection happens, the transfer MUST be reverted with the one caveat below.
    - If the tokens being sent are part of a hybrid implementation of another standard, that particular standard's rules on sending to a contract MAY now be followed instead. See "Compatibility with other standards" section.      
  
* A solidity example of the keccak256 generated constants for the return magic is:  
  - bytes4 constant public SRC1155_REJECTED = 0xafed434d; // keccak256("reject_src1155_tokens()")
  - bytes4 constant public SRC1155_ACCEPTED = 0x4dc21a2f; // keccak256("accept_src1155_tokens()")
  - bytes4 constant public SRC1155_BATCH_ACCEPTED = 0xac007889; // keccak256("accept_batch_src1155_tokens()")

##### Compatibility with other standards

There have been requirements during the design discussions to have this standard be compatible with older standards when sending to contract addresses, specifically SRC721 at time of writing. 
To cater for this there is some leeway with the rejection logic should a contract return `bytes4(keccack256("reject_src1155_tokens()"))` from the call to onSRC1155Received/onSRC1155BatchReceived as detailed in the main "Safe Transfer Rules" section above. 
In this case the hybrid implementation MAY now follow the secondary standard's rules when transferring token(s) to a contract address.

Note however it is recommended that a hybrid solution NOT be followed and a pure implementation of a single standard is followed instead, as a hybrid solution is an unproven method to date.

An example of a hybrid 1155+721 contract is linked in the references section under implementations.

### Metadata

The URI value allows for ID substitution by clients. If the string `{id}` exists in any URI, clients MUST replace this with the actual token ID in hexadecimal form. This allows for large number of tokens to use the same on-chain string by defining a URI once, for a large collection of tokens. Example of such a URI: `https://token-cdn-domain/{id}.json` would be replaced with `https://token-cdn-domain/000000000000000000000000000000000000000000000000000000000004cce0.json` if the client is referring to token ID 314592/0x4CCE0.

The string format of the substituted hexadecimal ID MUST be lowsrcase alphanumeric: `[0-9a-f]` with no 0x prefix.
The string format of the substituted hexadecimal ID MUST be leading zero padded to 64 hex characters length if necessary.

#### Metadata Extensions

The following optional extensions can be identified with the (SRC-165 Standard Interface Detection)[https://sips.superstring.ch/SIPS/sip-165].

Changes to the URI MUST emit the `URI` event if the change can be expressed with an event (i.e. it isn't dynamic). If the optional SRC1155Metadata_URI extension is included, the 'uri' function SHOULD be used to retrieve values for which no event was emitted. The function MUST return the same value as the event if it was emitted.

```solidity
pragma solidity ^0.5.8;

/**
    Note: The SRC-165 identifier for this interface is 0x0e89341c.
*/
interface SRC1155Metadata_URI {
    /**
        @notice A distinct Uniform Resource Identifier (URI) for a given token.
        @dev URIs are defined in RFC 3986.
        The URI may point to a JSON file that conforms to the "SRC-1155 Metadata JSON Schema".
        @return URI string
    */
    function uri(uint256 _id) external view returns (string memory);
}
```

#### SRC-1155 Metadata URI JSON Schema

This JSON schema is loosely based on the "SRC721 Metadata JSON Schema", but includes optional formatting to allow for ID substitution by clients. If the string `{id}` exists in any JSON value, it MUST be replaced with the actual token ID, by all client software that follows this standard.

The string format of the substituted hexadecimal ID MUST be lowsrcase alphanumeric: `[0-9a-f]` with no 0x prefix.

```json
{
    "title": "Token Metadata",
    "type": "object",
    "properties": {
        "name": {
            "type": "string",
            "description": "Identifies the asset to which this token represents",
        },
        "decimals": {
            "type": "integer",
            "description": "The number of decimal places that the token amount should display - e.g. 18, means to divide the token amount by 1000000000000000000 to get its user representation.",
        },
        "description": {
            "type": "string",
            "description": "Describes the asset to which this token represents",
        },
        "image": {
            "type": "string",
            "description": "A URI pointing to a resource with mime type image/* representing the asset to which this token represents. Consider making any images at a width between 320 and 1080 pixels and aspect ratio between 1.91:1 and 4:5 inclusive.",
        },
        "properties": {
            "type": "object",
            "description": "Arbitrary properties. Values may be strings, numbers, object or arrays.",
        },
    }
}
```

An example of an SRC-1155 Metadata JSON file follows. The properties array proposes some SUGGESTED formatting for token-specific display properties and metadata.

```json
{
	"name": "Asset Name",
	"description": "Lorem ipsum...",
	"image": "https:\/\/s3.amazonaws.com\/your-bucket\/images\/{id}.png",
	"properties": {
		"simple_property": "example value",
		"rich_property": {
			"name": "Name",
			"value": "123",
			"display_value": "123 Example Value",
			"class": "emphasis",
			"css": {
				"color": "#ffffff",
				"font-weight": "bold",
				"text-decoration": "underline"
			}
		},
		"array_property": {
			"name": "Name",
			"value": [1,2,3,4],
			"class": "emphasis"
		}
	}
}
```

##### Localization

Metadata localization should be standardized to increase presentation uniformity across all languages. As such, a simple overlay method is proposed to enable localization. If the metadata JSON file contains a `localization` attribute, its content MAY be used to provide localized values for fields that need it. The `localization` attribute should be a sub-object with three attributes: `uri`, `default` and `locales`. If the string `{locale}` exists in any URI, it MUST be replaced with the chosen locale by all client software.

##### JSON Schema

```json
{
    "title": "Token Metadata",
    "type": "object",
    "properties": {
        "name": {
            "type": "string",
            "description": "Identifies the asset to which this token represents",
        },
        "decimals": {
            "type": "integer",
            "description": "The number of decimal places that the token amount should display - e.g. 18, means to divide the token amount by 1000000000000000000 to get its user representation.",
        },
        "description": {
            "type": "string",
            "description": "Describes the asset to which this token represents",
        },
        "image": {
            "type": "string",
            "description": "A URI pointing to a resource with mime type image/* representing the asset to which this token represents. Consider making any images at a width between 320 and 1080 pixels and aspect ratio between 1.91:1 and 4:5 inclusive.",
        },
        "properties": {
            "type": "object",
            "description": "Arbitrary properties. Values may be strings, numbers, object or arrays.",
        },
        "localization": {
            "type": "object",
            "required": ["uri", "default", "locales"],
            "properties": {
                "uri": {
                    "type": "string",
                    "description": "The URI pattern to fetch localized data from. This URI should contain the substring `{locale}` which will be replaced with the appropriate locale value before sending the request."
                },
                "default": {
                    "type": "string",
                    "description": "The locale of the default data within the base JSON"
                },
                "locales": {
                    "type": "array",
                    "description": "The list of locales for which data is available. These locales should conform to those defined in the Unicode Common Locale Data Repository (http://cldr.unicode.org/)."
                }
            }
        },
    }
}
```

##### Localized Sample

Base URI:
```json
{
  "name": "Advertising Space",
  "description": "Each token represents a unique Ad space in the city.",
  "localization": {
    "uri": "ipfs://QmWS1VAdMD353A6SDk9wNyvkT14kyCiZrNDYAad4w1tKqT/{locale}.json",
    "default": "en",
    "locales": ["en", "es", "fr"]
  }
}
```

es.json:
```json
{
  "name": "Espacio Publicitario",
  "description": "Cada token representa un espacio publicitario único en la ciudad."
}
```

fr.json:
```json
{
  "name": "Espace Publicitaire",
  "description": "Chaque jeton représente un espace publicitaire unique dans la ville."
}
```

### Approval

The function `setApprovalForAll` allows an operator to manage one's entire set of tokens on behalf of the approver. To permit approval of a subset of token IDs, an interface such as [SRC-1761 Scoped Approval Interface (DRAFT)](https://sips.superstring.ch/SIPS/sip-1761) is suggested.
The counterpart `isAprrovedForAll` provides introspection into status set by `setApprovalForAll`.

An owner SHOULD be assumed to always be able to operate on their own tokens regardless of approval status, so should SHOULD NOT have to call `setApprovalForAll` to approve themselves as an operator before they can operate on them.  

## Rationale

### Metadata Choices

The `symbol` function (found in the SRC-20 and SRC-721 standards) was not included as we do not believe this is a globally useful piece of data to identify a generic virtual item / asset and are also prone to collisions. Short-hand symbols are used in tickers and currency trading, but they aren't as useful outside of that space.

The `name` function (for human-readable asset names, on-chain) was removed from the standard to allow the Metadata JSON to be the definitive asset name and reduce duplication of data. This also allows localization for names, which would otherwise be prohibitively expensive if each language string was stored on-chain, not to mention bloating the standard interface. While this decision may add a small burden on implementers to host a JSON file containing metadata, we believe any serious implementation of SRC-1155 will already utilize JSON Metadata.

### Upgrades

The requirement to emit `TransferSingle` or `TransferBatch` on balance change implies that a valid implementation of SRC-1155 redeploying to a new contract address MUST emit events from the new contract address to replicate the deprecated contract final state. It is valid to only emit a minimal number of events to reflect only the final balance and omit all the transactions that led to that state. The event emit requirement is to ensure that the current state of the contract can always be traced only through events. To alleviate the need to emit events when changing contract address, consider using the proxy pattern, such as described in SRC-1538. This will also have the added benefit of providing a stable contract address for users.

### Design decision: Supporting non-batch

The standard supports `safeTransferFrom` and `onSRC1155Received` functions because they are significantly cheaper for single token-type transfers, which is arguably a common use case.

### Design decision: Safe transfers only

The standard only supports safe-style transfers, making it possible for receiver contracts to depend on `onSRC1155Received` or `onSRC1155BatchReceived` function to be always called at the end of a transfer.

### Guaranteed log trace

As the Susy ecosystem continues to grow, many dapps are relying on traditional databases and explorer API services to retrieve and categorize data. The SRC-1155 standard guarantees that event logs emitted by the smart contract will provide enough data to create an accurate record of all current token balances. A database or explorer may listen to events and be able to provide indexed and categorized searches of every SRC-1155 token in the contract.

### Approval

The function `setApprovalForAll` allows an operator to manage one's entire set of tokens on behalf of the approver. It enables frictionless interaction with exchange and trade contracts.

Restricting approval to a certain set of Token IDs, quantities or other rules MAY be done with an additional interface or an external contract. The rationale is to keep the SRC-1155 standard as generic as possible for all use-cases without imposing a specific approval scheme on implementations that may not need it. Standard token approval interfaces can be used, such as the suggested [SRC-1761 Scoped Approval Interface](https://github.com/susytech/SIPs/issues/1761) which is compatible with SRC-1155.

## Usage

This standard can be used to represent multiple token types for an entire domain. Both Fungible and Non-Fungible tokens can be stored in the same smart-contract.

### Batch Transfers

The `safeBatchTransferFrom` function allows for batch transfers of multiple token ids and values. The design of SRC-1155 makes batch transfers possible without the need for a wrapper contract, as with existing token standards. This reduces gas costs when more than one token type is included in a batch transfer, as compared to single transfers with multiple transactions.

Another advantage of standardized batch transfers is the ability for a smart contract to respond to the batch transfer in a single operation using `onSRC1155BatchReceived`.

### Batch Balance

The `balanceOfBatch` function allows clients to retrieve balances of multiple owners and token ids with a single call.

### Enumerating from events

In order to keep storage requirements light for contracts implementing SRC-1155, enumeration (discovering the IDs and values of tokens) must be done using event logs. It is RECOMMENDED that clients such as exchanges and blockchain explorers maintain a local database containing the Token ID, Supply, and URI at the minimum. This can be built from each TransferSingle, TransferBatch, and URI event, starting from the block the smart contract was deployed until the latest block.

SRC-1155 contracts must therefore carefully emit TransferSingle or TransferBatch events in any instance where tokens are created, minted, or destroyed.

### Non-Fungible Tokens

The following strategy is an example of how to mix fungible and non-fungible tokens together in the same contract. The top 128 bits of the uint256 `_id` parameter in any SRC-1155 function could represent the base token ID, while the bottom 128 bits might be used for any extra data passed to the contract.

Non-Fungible tokens can be interacted with using an index based accessor into the contract/token data set. Therefore to access a particular token set within a mixed data contract and particular NFT within that set, `_id` could be passed as `<uint128: base token id><uint128: index of NFT>`.

Inside the contract code the two pieces of data needed to access the individual NFT can be extracted with uint128(~0) and the same mask shifted by 128.

### Example of split ID bits

```solidity
uint256 baseToken = 12345 << 128;
uint128 index = 50;

balanceOf(baseToken, msg.sender); // Get balance of the base token
balanceOf(baseToken + index, msg.sender); // Get balance of the Non-Fungible token index
```

## References

**Standards**
- [SRC-721 Non-Fungible Token Standard](https://sips.superstring.ch/SIPS/sip-721)
- [SRC-165 Standard Interface Detection](https://sips.superstring.ch/SIPS/sip-165)
- [SRC-1538 Transparent Contract Standard (DRAFT)](https://sips.superstring.ch/SIPS/sip-1538)
- [JSON Schema](https://json-schema.org/)
- [RFC 2119 Key words for use in RFCs to Indicate Requirement Levels](https://www.ietf.org/rfc/rfc2119.txt)

**Implementations**
- [SRC-1155 Reference Implementation](https://github.com/enjin/src-1155)
- [Horizon Games - Multi-Token Standard](https://github.com/horizon-games/multi-token-standard)
- [Enjin Coin](https://enjincoin.io) ([GitHub](https://github.com/enjin))
- [The Sandbox - Dual SRC-1155/721 Contract](https://github.com/pixowl/thesandbox-contracts/tree/master/src/Asset)

**Articles & Discussions**
- [Github - Original Discussion Thread](https://github.com/susytech/SIPs/issues/1155)
- [SRC-1155 - The Crypto Item Standard](https://blog.enjincoin.io/src-1155-the-crypto-item-standard-ac9cf1c5a226)
- [Here Be Dragons - Going Beyond SRC-20 and SRC-721 To Reduce Gas Cost by ~80%](https://medium.com/horizongames/going-beyond-src20-and-src721-9acebd4ff6ef)
- [Blockonomi - Susy SRC-1155 Token Perfect for Online Games, Possibly More](https://blockonomi.com/src1155-gaming-token/)
- [Beyond Gaming - Exploring the Utility of SRC-1155 Token Standard!](https://blockgeeks.com/src-1155-token/)

## Copyright
Copyright and related rights waived via [CC0](https://creativecommons.org/publicdomain/zero/1.0/).
